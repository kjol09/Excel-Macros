Sub LoopAllExcelFilesInFolder ()

'Purpose: To loop through all Excel files in a folder and perform a set task on them

Dim wb As Workbook
Dim myPath As String
Dim myFile As String
Dim myExtension As String
Dim FldrPicker As FileDialog

'Optimise Macro Speed
Application.ScreenUpdating = False
Application.EnableEvents = False
Application.Calculation = xlCalculationManual

'Retrieve Target Folder Path From User
Set FldrPicker = Application.FileDialog(msoFileDialogFolderPicker)

	With FldrPicker
	.Title = "C:\Users\chongyuang\Desktop\acd"
	.AllowMultiSelect = False
	  If .Show <> -1 Then GoTo NextCode
	  myPath = .SelectedItems(1) & "\"
	End With

'In Case of Cancel
NextCode:
  myPath = myPath
  If myPath = "" Then GoTo ResetSettings

'Target File Extension (must include wildcard "*")
  myExtension = "*.xlsx"

'Target Path with Ending Extension
  myFile = Dir(myPath & myExtension)

'Loop through each Excel file in folder
  Do While myFile <> ""
	'Set variable equal to opened workbook
	  Set wb = Workbooks.Open(Filename:=myPath & myFile)

	'Change First Worksheet's Background Fill Blue
	  wb.Worksheets(1).Range("A1:Z1").Interior.Color = RGB(51,98,174)

	'Save and Close Workbook
	  wb.Close SaveChanges:=True

	'Get next file name
	  myFile = Dir
  Loop

'Message Box when tasks are completed
  MsgBox "Task Complete!"

ResetSettings:
  'Reset Macro Optimization Settings
	Application.EnableEvents = True
	Application.Calculation = xlCalculationAutomatic
	Application.ScreenUpdating = True

End Sub
